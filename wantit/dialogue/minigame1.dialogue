~ minigame1_start
Na toll, jetzt müssen wir die Klebezettel wieder den richtigen Tütchen zuordnen.
Die Abbildungen der gesicherten Spuren und die Klebezettelchen stellen Daten dar. 
Untersuche die Spuren mit der Lupe, um herauszufinden, welche Informationen durch die Daten repräsentiert werden.
=> END

~ minigame1_end
Geschafft!
=> minigame1_test1

~ minigame1_test1
Die gesicherten Spuren sind...
- Daten => minigame1_test2
- Informationen => minigame1_test1
- Ich verstehe die Begriffe nicht. => explain_test1
=> END

~ explain_test1
Daten sind einzelne Dinge wie Wörter, Zahlen, Bilder, Töne und Ähnliches, die für sich allein noch nichts sagen. 
Eine Information entsteht, wenn diese Daten für uns eine Bedeutung bekommen.
=> minigame1_test1

~ minigame1_test2
Mittels der Lupe konnte ich sie...
- interpretieren => minigame1_test3
- repräsentieren => explain_test2
- Ich verstehe die Begriffe nicht. => explain_test2

~ explain_test2
Interpretieren bedeutet, etwas so zu verstehen, dass man den Sinn oder die Absicht dahinter erkennt.
Repräsentieren hingegen bedeutet, eine Darstellung durch eine andere Darstellung so abzubilden, dass sie stellvertretend für das Original stehen.
=> minigame1_test2

~ minigame1_test3
Die gewonnene Information konnte wiederum durch die Zettelchen...
- interpretiert werden => explain_test3
- repräsentiert werden => add_interaction

~ explain_test3
Die Notizzettel helfen, die Information darzustellen.
Interpretieren bedeutet, etwas so zu verstehen, dass man den Sinn oder die Absicht dahinter erkennt.
Repräsentieren hingegen bedeutet, eine Darstellung durch eine andere Darstellung so abzubilden, dass sie stellvertretend für das Original stehen.
=> minigame1_test3

~ add_interaction
do interaction_happened("Minigame1 completed") 
=> return_from_minigame

~ return_from_minigame
Klaas: Ich werde noch Fingerabdrücke von allen Angestellten nehmen. Danach können wir zurück ins Präsidium.
do _on_location_switch_requested("Office")
=> END
